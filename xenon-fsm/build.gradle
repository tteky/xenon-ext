apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

configurations {
    provided
}
repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.vmware.xenon', name: 'xenon-slf4j', version: xenonVersion
    compile group: 'com.vmware.xenon', name: 'xenon-common', version: xenonVersion
    compile project(':jaxrs-support')

    testCompile group: 'junit', name: 'junit', version: '4.11'
}



task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossUser, password: ossPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossUser, password: ossPassword)
            }

            pom.project {
                name 'Xenon Extension : State machine for Xenon'
                packaging 'jar'

                description 'State machine using Xenon Stateful service'
                url 'https://github.com/tteky/xenon-ext'

                pom.version = version
                pom.artifactId = "xenon-fsm"
                pom.groupId = 'com.github.tteky'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}